namespace com.ibm.streamsx.testframe;

public composite FileSink1 (input Inp) {
	param
		expression<rstring> $filenameTuples: "Tuples";
		expression<rstring> $filenameWidowMarker: "WindowMarker";
		expression<rstring> $filenameFinalMarker: "FinalMarker";
		expression<boolean> $singleTupleFiles: true;
	graph
		stream<I, tuple<rstring fname>> FileContent as O = Custom(Inp as I) {
			logic
				state : {
					mutable int32 tcount = 0;
				}
				onTuple I : {
					mutable O ot = {};
					assignFrom(ot, I);
					if ($singleTupleFiles) {
						ot.fname = $filenameTuples;
					} else {
						ot.fname = $filenameTuples + (rstring)tcount;
					}
					submit(ot, O);
					tcount = tcount + 1;
				}
				onPunct I: {
					mutable O ot = {};
					if (currentPunct() == Sys.WindowMarker) {
						ot.fname = $filenameWidowMarker;
					} else {
						ot.fname = $filenameFinalMarker;
					}
					submit(ot, O);
				}
		}
		
		() as Sink = FileSink(FileContent as I) {
			param
				closeMode: dynamic;
				file: fname;
				format: txt;
				suppress: fname;
		}
}